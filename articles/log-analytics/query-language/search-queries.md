---
title: Поисковые запросы в Log Analytics | Документация Майкрософт
description: В этой статье содержится руководство по написанию поисковых запросов в Log Analytics.
services: log-analytics
documentationcenter: ''
author: bwren
manager: carmonm
editor: ''
ms.assetid: ''
ms.service: log-analytics
ms.workload: na
ms.tgt_pltfrm: na
ms.devlang: na
ms.topic: conceptual
ms.date: 08/06/2018
ms.author: bwren
ms.component: na
ms.openlocfilehash: 250eddb043ccf9fa0b1bb92a298900f8ad820140
ms.sourcegitcommit: 32d218f5bd74f1cd106f4248115985df631d0a8c
ms.translationtype: HT
ms.contentlocale: ru-RU
ms.lasthandoff: 09/24/2018
ms.locfileid: "46992275"
---
# <a name="search-queries-in-log-analytics"></a>Поисковые запросы в Log Analytics

> [!NOTE]
> Прежде чем приступать к этому уроку, необходимо ознакомиться с руководством по [использованию запросов в Azure Log Analytics](get-started-queries.md).

[!INCLUDE [log-analytics-demo-environment](../../../includes/log-analytics-demo-environment.md)]

Запросы Azure Log Analytics могут начинаться с имени таблицы или команды search. В этом руководстве рассматриваются запросы на основе поиска. У каждого метода есть свои преимущества.

Запросы на основе таблиц начинаются с определения области запроса и, следовательно, обычно более эффективны, чем поисковые запросы. Поисковые запросы менее структурированы, поэтому они являются лучшим выбором при поиске определенного значения в пределах столбцов и таблиц. Команда **search** может сканировать все столбцы в данной таблице или во всех таблицах в поисках указанного значения. Объем обрабатываемых данных может быть огромным, поэтому выполнение этих запросов может занять больше времени и могут возвращаться очень большие результирующие наборы.

## <a name="search-a-term"></a>Поиск термина
Команда **search** обычно используется для поиска конкретного термина. В следующем примере все столбцы во всех таблицах проверяются на наличие термина "error":

```Kusto
search "error"
| take 100
```

Хотя они просты в использовании, не ограниченные областью запросы, такие как показанные выше, неэффективны и часто возвращают множество нерелевантных результатов. Рекомендуется выполнять поиск в соответствующей таблице или даже в определенном столбце.

### <a name="table-scoping"></a>Определение конкретной таблицы
Чтобы выполнить поиск термина в отдельной таблице, добавьте `in (table-name)` сразу после оператора **search**:

```Kusto
search in (Event) "error"
| take 100
```

Для поиска в нескольких таблицах:
```Kusto
search in (Event, SecurityEvent) "error"
| take 100
```

### <a name="table-and-column-scoping"></a>Определение конкретной таблицы и столбца
По умолчанию команда **search** проверит все столбцы в наборе данных. Чтобы выполнить поиск только в определенном столбце, используйте следующий синтаксис:

```Kusto
search in (Event) Source:"error"
| take 100
```

> [!TIP]
> При использовании `==` вместо `:` результаты будут включать записи, в которых столбец *Source* имеет точное значение "error". При использовании ":" записи из столбца *Source* с такими значениями, как "error code 404" или "Error", не будут включены.

## <a name="case-sensitivity"></a>Учет регистра
По умолчанию в поисковом термине не учитывается регистр, поэтому поиск "dns" может дать такие результаты, как "DNS", "dns" или "Dns". Чтобы при поиске учитывался регистр, используйте параметр `kind`:

```Kusto
search kind=case_sensitive in (Event) "DNS"
| take 100
```

## <a name="use-wild-cards"></a>Использование подстановочных знаков
Команда **search** поддерживает подстановочные знаки в начале, в конце или в середине термина.

Чтобы выполнить поиск терминов, которые начинаются с "win":
```Kusto
search in (Event) "win*"
| take 100
```

Чтобы выполнить поиск терминов, которые заканчиваются на ".com":
```Kusto
search in (Event) "*.com"
| take 100
```

Чтобы выполнить поиск терминов, которые содержат "www":
```Kusto
search in (Event) "*www*"
| take 100
```

Чтобы выполнить поиск терминов, которые начинаются с "corp" и заканчиваются на ".com", например "corp.mydomain.com":

```Kusto
search in (Event) "corp*.com"
| take 100
```

Вы также можете получить все записи в таблице, используя только подстановочный знак `search in (Event) *`. Результат будет такой же, как если написать просто `Event`.

> [!TIP]
> Хотя `search *` можно использовать для получения каждого столбца из каждой таблицы, рекомендуется всегда ограничивать свои запросы определенными таблицами. Выполнение неограниченных запросов может занять время и вернуть слишком много результатов.

## <a name="add-and--or-to-search-queries"></a>Добавление операторов *and* и / *or* в поисковые запросы
Используйте оператор **and** для поиска записей, которые содержат несколько терминов:

```Kusto
search in (Event) "error" and "register"
| take 100
```

Используйте оператор **or**, чтобы получить записи, которые содержат хотя бы один из терминов:

```Kusto
search in (Event) "error" or "register"
| take 100
```

Если у вас есть несколько условий поиска, их можно объединить в один запрос с помощью скобок:

```Kusto
search in (Event) "error" and ("register" or "marshal*")
| take 100
```

Результатом этого примера будут записи, содержащие термин "error" и "register" или что-нибудь, что начинается с "marshal".

## <a name="pipe-search-queries"></a>Действия с поисковыми запросами
Как и с любыми другими командами, с командой **search** можно задать параметр, чтобы фильтровать, сортировать и вычислять результаты поиска. Например, чтобы получить число записей *Event*, которые содержат "win":

```Kusto
search in (Event) "win"
| count
```




## <a name="next-steps"></a>Дополнительная информация

- Дополнительные руководства см. на [сайте языка запросов Log Analytics](https://aka.ms/LogAnalyticsLanguage)